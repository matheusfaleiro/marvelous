plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlin-parcelize'
    id 'com.google.gms.google-services'
}

apply from: '../config/detekt/detekt.gradle'

def apikeyPropertiesFile = rootProject.file("api_key.properties")
def apikeyProperties = new Properties()
apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"
    namespace 'dev.theuzfaleiro.marvelous'

    defaultConfig {
        applicationId "dev.theuzfaleiro.marvelous"
        minSdk 26
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "dev.theuzfaleiro.marvelous.runner.MarvelousRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'

        buildConfigField "String", "PUBLIC_KEY", apikeyProperties['PUBLIC_KEY']
        buildConfigField "String", "PRIVATE_KEY", apikeyProperties['PRIVATE_KEY']
        buildConfigField "String", "BASE_URL", '"https://gateway.marvel.com/v1/public/"'
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    signingConfigs {
        release {
            keyAlias 'marvelous'
            keyPassword '**********'
            storePassword '**********'
            storeFile file('/Users/matheusfaleiro/Documents/Personal/Android/marvelous/playstore/marvelous_keys')
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
        staging {
            initWith debug
            applicationIdSuffix ".staging"
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro',
                    'proguard-rules-staging.pro'
        }
        release {
            applicationIdSuffix ".release"
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation project(':core-network')
    testImplementation project(':testing')

    implementation library.coreKtx
    implementation androidLibrary.appCompat
    implementation androidLibrary.material
    implementation androidLibrary.constraintLayout
    implementation androidLibrary.recyclerView

    implementation androidLibrary.navigationUi
    implementation androidLibrary.navigationFragment

    implementation androidLibrary.lifecycleViewModel
    implementation androidLibrary.lifecycleLiveData
    implementation androidLibrary.lifecycleRuntime

    implementation platform('com.google.firebase:firebase-bom:29.3.0')
    implementation 'com.google.firebase:firebase-analytics'

    implementation 'com.facebook.shimmer:shimmer:0.5.0'

    implementation "androidx.paging:paging-runtime:3.1.1"

    def hiltVersion = '2.41'
    implementation "com.google.dagger:hilt-android:$hiltVersion"
    kapt "com.google.dagger:hilt-compiler:$hiltVersion"

    implementation(platform("com.squareup.okhttp3:okhttp-bom:4.9.0"))
    implementation("com.squareup.okhttp3:okhttp")
    implementation("com.squareup.okhttp3:logging-interceptor")

    implementation library.retrofit
    implementation library.retrofitConverter

    implementation "io.coil-kt:coil:1.4.0"

    def fragmentVersion = "1.4.1"
    debugImplementation "androidx.fragment:fragment-testing:$fragmentVersion"

    androidTestImplementation "com.google.dagger:hilt-android-testing:$hiltVersion"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$hiltVersion"

    androidTestImplementation testAndroidLibrary.jUnit
    androidTestImplementation testAndroidLibrary.espresso

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestUtil 'androidx.test:orchestrator:1.4.1'

    androidTestImplementation "com.squareup.okhttp3:mockwebserver:4.9.3"
}